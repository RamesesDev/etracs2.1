<?xml version="1.0" encoding="UTF-8"?>
<workunit>
    <invokers>
        <invoker type="faasattachment.open" action="open" />
        <invoker type="formActions" action="_close" caption="Back" mnemonic="b" shortcut="ctrl B" icon="images/back.png" />
        <invoker type="formActions" action="addAttachment" caption="Add" mnemonic="a" shortcut="ctrl A" icon="images/doc-add.png" />
        
        <invoker type="viewActions" action="_default" caption="Back" mnemonic="b" shortcut="ctrl B" icon="images/back.png" />
    </invokers>
    <code lang="groovy">
<![CDATA[

import com.rameses.rcp.annotations.*
import com.rameses.rcp.common.*
import com.rameses.osiris2.client.*
import java.rmi.server.UID
import com.rameses.io.FileTransfer
import com.rameses.invoker.client.DynamicHttpTransfer
import java.io.*
import etracs2.groovy.*
import etracs2.rpt.groovy.*
import com.rameses.rcp.framework.TaskManager;


public class FAASAttachmentController {
    @Binding
    def binding
    
    @Service('FAASService')
    def faasSvc
    
    @Service('ETRACSFileTransferService')
    def svc 

    def faas 
    def selectedItem
    def attachments
    def caption 
    
    
    void open() {
        caption = 'Attachment for PIN No. ' + faas.rpu.fullpin 
        attachments = faasSvc.getAttachments( faas.objid )
    }
    
    def getHtml() {
        return faasSvc.buildHtml( faas.objid )
    }
    
    def listHandler = [
        getRows    : { return 25 },
        getColumns : { return [
            new Column(name:'type', caption:'Attachment'),
            new Column(name:'remarks', caption:'Remarks'),
        ]},
        onRemoveItem : { item -> removeItem( item ) },
        fetchList : { return attachments },
    ] as SubListModel
    
    void removeItem( item ) {
        if( MsgBox.confirm('Delete selected item?') ) {
            faasSvc.removeAttachment( faas.objid, item ) 
            attachments.remove( item )
        }
    }
    
    void setSelectedItem( selectedItem ) {
        this.selectedItem = selectedItem
        if( selectedItem ) {
            def file = new File( resolveFileName( selectedItem.filename ) )
            if( ! file.exists() ) {
                downloadFile( selectedItem )
            }
        }
    }

    void removeCachedDirectory() {
        def cacheinitialized = OsirisContext.clientContext.properties.cache_initialized
        if( cacheinitialized ) return 
        try {
            def cachedir = getCacheDirectory()
            def file = new File( cachedir )
            if( file.isDirectory() ) {
                file.listFiles().each { f ->
                    f.delete()
                }
            }
        }
        catch( e ) {
            println 'removeCachedDirectory() error'
            e.printStackTrace()
        }
    }
    
    def resolveFileName( filename ) {
        def cachedir = getCacheDirectory()
        def dir = new File( cachedir )
        if( !dir.exists() ) {
            dir.mkdir()
            OsirisContext.clientContext.properties.cache_initialized = true 
        }
        return cachedir + '/' + filename 
    }
    
    def getCacheDirectory() {
        return System.properties['user.dir'] + '/cache' 
    }
    
    def getImageUrl() {
        return new File( resolveFileName(attachment.filename) )
    }
    
    def displayAttachment( attachmentid ) {
        attachment = faasSvc.getAttachmentById( attachmentid )
        if( attachment ) {
            def file = new File( resolveFileName( attachment.filename ) )
            if( ! file.exists() ) {
                downloadFile( attachment )
            }
        }
        return 'view'
    }
    
    /*----------------------------------------------------------------------
    *
    * Attachment Support 
    *
    *-----------------------------------------------------------------------*/
    def attachment
    def file
    
    
    void removeAttachment( attachmentid ) {
        if( MsgBox.confirm('Delete attachment?') ) {
            faasSvc.removeAttachment( faas.objid, attachmentid )
        }
    }
    
    def addAttachment() {
        attachment = [objid: 'FA' + new UID()]
        return 'attachment' 
    }
    
    
    /* ========================================================== */
    /*  Temporary : create this as a helper class */
    /* ========================================================== */
    
    def msg
    def displaymsg 
    def stopped = true 
    def taskmgr 
    def timertask
    def uploadTask
            
    def ontimer = {
        if( ! stopped ) {
            updateMessage()
        }
    }
    
    void updateMessage() {
        if( displaymsg ) {
            displaymsg = false
            showMessage('')
        }
        else {
            displaymsg = true
            showMessage( 'Uploading document. Please wait...'  )
        }
    }
    
    void showMessage( msg ) {
        this.msg = msg
        binding.refresh('msg')
    }
        
    def doCancel() {
        if( ! stopped ) {
            stopTimer()
        }
        else {
            return 'default'
        }
    }
    
    
    void startTimer() {
        stopped = false
        taskmgr = new TaskManager()
        timertask = new TTimerTask( ontimer : ontimer, sleeptime:1000)
        uploadTask = new UploadTask( parent:this )
        taskmgr.addTask( timertask )
        taskmgr.addTask( uploadTask )
        taskmgr.start()   
    }
    
    void stopTimer( ) {
        stopped = true 
        displaymsg = false
        timertask.setCancelled( true )
        timertask.setEnded( true )
        uploadTask.setCancelled( true )
        uploadTask.setEnded(true)
        taskmgr?.removeTask( timertask )
        taskmgr?.removeTask( uploadTask )
        taskmgr?.stop()
        taskmgr = null
        timertask = null
        showMessage('')
    }

    
    
    void upload() {
        validateFile()
        startTimer()
    }
    
    void validateFile() {
        // accept jpg, jpeg, png, bmp extension only
        def extn = getFileExtension( file )
        extn = extn.replace('.','')
        if( 'jpg/jpeg/png/bmp/gif/pdf'.indexOf( extn ) < 0 ) {
            throw new Exception('File to upload is invalid.\nOnly the following extensions are supported: jpg, jpeg, png, bmp, gif and pdf.')
        }
    }
    
    def getFileExtension( file ) {
        def ext = ''
        if( file && file.exists()) {
            def filename = file.absolutePath
            int idx = filename.lastIndexOf(".")
            if( idx >= 0 ) {
                ext = '.' + filename.substring(idx+1, filename.length())
            }
        }
        return ext.toLowerCase()
    }
    
    void copyToCache( file ) {
        FileTransfer.FileInputSource fis = new FileTransfer.FileInputSource( file )
        
        File f = new File( resolveFileName(attachment.filename) );
        FileTransfer.FileOutputHandler output = new FileTransfer.FileOutputHandler( f );
        
        FileTransfer ft = new FileTransfer();
        ft.transfer( fis, output );
    }
    
    void downloadFile( attachment ) {
        def app = getAppInfo()
        
        DynamicHttpTransfer.In handler = new DynamicHttpTransfer.In( app.host, app.context, 'ETRACSFileTransferService', 'downloadAttachment');
        handler.getParameters().put('attachment', attachment);
        
        File f = new File( resolveFileName(attachment.filename) );
        FileTransfer.FileOutputHandler output = new FileTransfer.FileOutputHandler( f );
        
        FileTransfer ft = new FileTransfer();
        ft.transfer( handler, output );
    }
    
    def getAppInfo() {
        def host        = OsirisContext.clientContext?.appEnv['app.host']
        host            = ( host ? host : 'localhost:8080' )
        def context     = OsirisContext.clientContext?.appEnv['app.context']
        context         = (context ? context : 'lguname' )
        return [host:host, context:context]
    }
    
    def makeSafeFileName( strvalue ) {
        return strvalue.replaceAll('[\\W]', '')
    }
    
}

class UploadTask extends com.rameses.rcp.common.Task {
    def ended = false
    def cancelled = false 
    def parent 

    public boolean accept() {
       return !ended && !cancelled 
    }

    public boolean isEnded() {
        return ended || cancelled 
    }

    public void execute() {
        try {
            def attachment  = parent.attachment 
            def faas        = parent.faas 
            def app         = parent.getAppInfo()
            
            attachment.filename  = parent.makeSafeFileName( attachment.objid ) + parent.getFileExtension( parent.file )
            attachment.directory = parent.makeSafeFileName( faas.objid ) 

            FileTransfer.FileInputSource fis = new FileTransfer.FileInputSource( parent.file )
            DynamicHttpTransfer.Out handler = new DynamicHttpTransfer.Out(app.host , app.context, 'ETRACSFileTransferService', 'uploadAttachment')
            handler.parameters.put('attachment', attachment )

            FileTransfer ft = new FileTransfer()
            ft.transfer(fis, handler)
            
            parent.svc.uploadComplete( attachment )
            parent.copyToCache( parent.file )
            parent.faasSvc.addAttachment( faas.objid, attachment )
            parent.stopTimer()
            parent.attachments.add( attachment )
            parent.listHandler.load()
            parent.binding.fireNavigation('default')
        }
        catch( e ) {
            e.printStackTrace()
        }
    }
}  



]]>
    </code>
    
    <pages>
        <page template="etracs2.rpt.faas.FAASAttachmentPage"/>
        <page name="attachment" template="etracs2.rpt.faas.FAASAttachmentAddPage"/>
        <page name="view" template="etracs2.rpt.faas.FAASAttachmentViewPage"/>
    </pages>
</workunit>
