<?xml version="1.0" encoding="UTF-8"?>
<workunit>
    
    <invokers>
        <invoker folderid="/explorer/txn/treasury/collection" action="create" caption="Remit" role="COLLECTOR" permission="remittance.create" index="20" />
        <invoker type="home.action" action="create" caption="Remit" role="COLLECTOR" permission="remittance.create" index="110" icon="images/folder.png" target="window"/>
        
        <invoker type="quick_launcher" code="REMIT01" action="create" caption="Remit" role="COLLECTOR" permission="remittance.create"/>
        
        <invoker type="formActions" action="close"  immediate="true" caption="Close" mnemonic="c" icon="images/cancel.png"/>
        <invoker type="formActions" action="create" shortcut="ctrl N" visibleWhen="#{mode == 'view'}" immediate="true" caption="New" mnemonic="n" icon="images/doc-new.png" role="COLLECTOR" permission="remittance.create"/>
        <invoker type="formActions" action="next" shortcut="ctrl N" visibleWhen="#{mode == 'init'}" caption="Next" mnemonic="n" icon="images/next.png"/>
        <invoker type="formActions" action="back" shortcut="ctrl B" visibleWhen="#{mode == 'main'}" caption="Back" mnemonic="b" icon="images/back.png"/>
        <invoker type="formActions" action="submit" shortcut="ctrl S" visibleWhen="#{mode == 'main'}" caption="Submit" mnemonic="s" icon="images/doc-submit.png" />
        <invoker type="formActions" action="viewRCD" shortcut="ctrl R" visibleWhen="#{mode == 'view'}" immediate="true" caption="View RCD" mnemonic="r" icon="images/doc-view.png"/>
        <invoker type="formActions" action="exportRemittance" shortcut="ctrl E" visibleWhen="#{mode == 'view'}" immediate="true" caption="Export Remittance" mnemonic="r" icon="images/doc-view.png" />
        <invoker type="formActions" action="viewOtherReports" shortcut="ctrl O" visibleWhen="#{mode == 'view'}" immediate="true" caption="Other Reports" mnemonic="o" icon="images/otherReports.png" />
        <invoker type="formActions" action="backToMain" shortcut="ctrl B" visibleWhen="#{mode == 'reports' or mode == 'rcd'}" immediate="true" caption="Back" mnemonic="b" icon="images/back.png"/>
        <invoker type="formActions" action="viewReport" shortcut="ctrl V" visibleWhen="#{mode == 'reports'}" immediate="true" caption="View" mnemonic="v" icon="images/doc-view.png" />
    </invokers>
    
    <code lang="groovy">
        <![CDATA[
        import com.rameses.rcp.common.*;
        import com.rameses.rcp.annotations.*;
        import com.rameses.osiris2.client.*;
        import java.text.DecimalFormat;
        import java.io.*
        import javax.swing.JFileChooser;
        import com.rameses.util.*
        import java.rmi.server.UID
        
        public class RemittanceController {
        
            @Service('RemittanceService')
            def svc;
            
            @Service('ETRACSUserService')
            def usersvc;
            
            @Service("DateService")
            def dtsvc;
            
            def entity;
            
            @Caller
            def caller;
            
            @Binding
            def binding;
            
            def mode;
            
            def numformat = new DecimalFormat("#,##0.00");
            
            def collector;
        
            def denominationOpener;
            
            def selectedreport;
            
            def reports;
            def lqofficer
            def remittanceAmount
            def collectionSummaries 
            
            def create() {
                collector = usersvc.getCollector( OsirisContext.env.USERID );
                entity = createEntity() 
                mode = 'init'
                collectionSummaries = svc.getOpenCollectionSummaries( OsirisContext.env.USERID ) 
                remittanceAmount = 0.0
                if( collectionSummaries ) {
                    remittanceAmount = collectionSummaries.amount.sum()
                }
                
                return 'init';
            }
            
            def createEntity() {
                return [
                    objid               : 'REM' + new UID(),
                    collectorid         : collector.objid,
                    txndate             : dtsvc.getServerDate(),
                    collectorname       : collector.formalname,
                    collectortitle      : collector.jobtitle,
                    amount              : 0.00,
                    totalcash           : 0.00,
                    totalotherpayment   : 0.00,
                    remittedforms       : [],
                ]
            }
            
            def summaryHandler = [
                getColumns : { return [
                    new Column(name:'afid', caption:'AF No.', width:50 ),
                    new Column(name:'stubno', caption:'Stub No.', width:80 ),
                    new Column(name:'fromserialno', caption:'From', width:120 ),
                    new Column(name:'toserialno', caption:'To', width:120 ),
                    new Column(name:'amount', caption:'Amount', type:'decimal', format:'#,##0.00'),
                ]},
                fetchList  : { return collectionSummaries }
            ] as SubListModel 
            
            def open() {
                if (entity)
                    entity = svc.open( entity.objid );
                mode = 'view';
                denominationOpener = InvokerUtil.lookupOpener('cashreceipt:breakdown', [totalAmount:entity.totalcash, list:entity.denominations, mode:'view']);
                
                return 'main';
            }
            
            def next() {
                updateLiquidatingOfficerInfo()
                entity = svc.createRemittance( entity );
                denominationOpener = InvokerUtil.lookupOpener('cashreceipt:breakdown', [totalAmount:entity.totalcash, list:entity.denominations]);
                mode = 'create' 
                return 'main';
            }
            
            void updateLiquidatingOfficerInfo() {
                entity.liquidatingofficerid = lqofficer.objid
                entity.liquidatingofficername = lqofficer.formalname
                entity.liquidatingofficertitle = lqofficer.jobtitle
            }
            
            def back() {
                mode = 'init';
                return 'init';
            }
            
            def submit() {
                if (! MsgBox.confirm( "Submit remittance to $entity.liquidatingofficername?" ) )
                    return null;
                entity.denominations = denominationOpener.handle.list;
                entity = svc.save( entity );
                if (caller)
                    caller.binding.refresh();
                mode = 'view';
                denominationOpener.handle.mode = 'view' 
                binding.refresh();
                return 'main';
            }
            
            void exportRemittance() {
                if( ! MsgBox.confirm('Export remittance?') ) return;
                
                def previouspath = System.getProperty("chooser.previouspath");
                JFileChooser chooser = new JFileChooser( previouspath );
                chooser.fileSelectionMode = JFileChooser.FILES_ONLY;
                def sdf = new java.text.SimpleDateFormat("yyyyMMddHHmmss");
                def fileName = entity.txnno + ".rem";
                chooser.selectedFile = new File( fileName );
                int res = chooser.showSaveDialog( null );
                if( res == JFileChooser.APPROVE_OPTION ){
                    def data = [
                        remittance         : svc.exportRemittance( entity.objid ),   
                        remittedforms      : svc.exportRemittedForms( entity.objid ),   
                        revenues           : svc.exportRevenues( entity.objid ),   
                        receiptlists       : svc.exportReceiptLists( entity.objid ),   
                        receiptitems       : svc.exportReceiptItems( entity.objid ),                                           
                        paymentitems       : svc.exportPaymentItems( entity.objid ),  
                        afcontrols         : svc.exportAFControls( entity.objid),
                        craafcredits       : svc.exportCraafCredits( entity.objid ),
                    ]

                    def out = null 
                    def oos = null 
                    try {
                        data = ObjectSerializer.instance.toString( data )
                        data = CipherUtil.encode(data)
                        out = chooser.selectedFile.newOutputStream()
                        oos = new ObjectOutputStream( out )
                        oos.writeObject( data )
                        oos.close()
                        System.setProperty("chooser.previouspath", chooser.selectedFile.parent )
                        MsgBox.alert( "Remittance No. $entity.txnno was successfully exported." )
                    }
                    catch( e) {
                        throw e 
                    }
                    finally{
                        try{ out.close() }catch(e) { }
                    }
                }                
            }
            
            
            def close() {
                return '_close';
            }
            
            
            def viewOtherPayments() {
                if( entity.otherpayments == null ) {
                    if( mode == 'create' )
                        entity.otherpayments = svc.getOpenNonCashPayments( entity )
                    else 
                        entity.otherpayments = svc.getRemittedNonCashPayments( entity )
                }
                return new PopupOpener( outcome:'otherpayments' );
            }
            
            def viewAccountableForms() {
                return new PopupOpener( outcome:'remittedforms' );
            }
            
            def viewReceipts() {
                if( entity.receipts == null ) {
                    if( mode == 'create' )
                        entity.receipts = svc.getUnremittedReceipts( entity.collectorid )
                    else 
                        entity.receipts = svc.getRemittanceReceipts( entity.objid )
                }
                return new PopupOpener( outcome:'receipts' );
            }
            
            def otherPaymentHandler = [
                getRows    : { return 50; },
                getColumns : {
                    return [
                        new Column( name:"paytype", caption:"Type" ),
                        new Column( name:"particulars", caption:"Info" ),
                        new Column( name:"amount", caption:"Amount", format: "#,##0.00", width:100 )
                    ];
                },
                fetchList : { return entity.otherpayments; }
            ] as PageListModel;
            
            def getTotalOtherPayment() {
                return numformat.format( entity.totalotherpayment );
            }
            
            def receiptHandler = [
                
                getRows    : { return 50; },
                
                getColumns : {
                    return [
                        new Column( name:"collectiontype", caption:"Type" ),
                        new Column( name:"serialno", caption:"Serial No." ),
                        new Column( name:"txndate", caption:"Txn Date", format: "yyyy-MM-dd" ),
                        new Column( name:"mode", caption:"Mode" ),
                        new Column( name:"paidby", caption:"Payor" ),
                        new Column( name:"paidbyaddress", caption:"Payor Address" ),
                        new Column( name:"voided", caption:"Voided" ),
                        new Column( name:"amount", caption:"Amount", format: "#,##0.00", width:100 )
                    ];
                },
                
                fetchList : { 
                    return entity.receipts;
                }
            
            ] as PageListModel;
            
            def getTotalAmount() {
                return numformat.format( entity.amount );
            }
            
            def remittedFormHandler = [
                
                getRows    : { return 50; },
                
                getColumns : {
                    return [
                        new Column( name:"afid", caption:"AF No."),
                        new Column( name:"stubno", caption:"Stub No." ),
                        new Column( name:"beginfrom", caption:"Begin From"),
                        new Column( name:"beginto", caption:"Begin To"),
                        new Column( name:"beginqty", caption:"Begin Qty"),
                        new Column( name:"receivedfrom", caption:"Received From"),
                        new Column( name:"receivedto", caption:"Received To"),
                        new Column( name:"receivedqty", caption:"Received Qty"),
                        new Column( name:"issuedfrom", caption:"Issued From"),
                        new Column( name:"issuedto", caption:"Issued To"),
                        new Column( name:"issuedqty", caption:"Issued Qty"),
                        new Column( name:"endingfrom", caption:"Ending From"),
                        new Column( name:"endingto", caption:"Ending To"),
                        new Column( name:"endingqty", caption:"Ending Qty")
                    ];
                },
                
                fetchList : { 
                    return entity.remittedforms;
                }
            
            ] as PageListModel;
            
            
            def viewRCD() {
                return new Opener( name:'tc2:remittance_report_rcd', action:'view', params:[entity:entity] );
            }
            
            def getReportList() {
                if( !reports ) reports =  InvokerUtil.lookupOpeners("remittance.report", [entity: entity] );
                return reports;
            } 
            
            def viewOtherReports() {
                mode = 'reports';
                return 'otherreports';
            }
            
            def viewReport() {
                if (!selectedreport) return null;
                return selectedreport;
            }
            
            def backToMain() {
                mode = 'view';
                return 'main';
            }
            
            List getLqofficerlist() {
                return usersvc.getLiquidatingOfficers(null, null)
            }

        }
        
        ]]>
    </code>
    
    <pages>
        <!--page name="view" template="etracs2.tc.HTMLViewPage" /-->
        <page name="main" template="etracs2.tc.remittance.RemittancePage" />
        <page name="init" template="etracs2.tc.remittance.RemittanceInitPage" />
        <page name="otherpayments" template="etracs2.tc.remittance.OtherPaymentListPage" />
        <page name="remittedforms" template="etracs2.tc.remittance.RemittedFormListPage" />
        <page name="receipts" template="etracs2.tc.remittance.ReceiptListPage" />
        <page name="otherreports" template="etracs2.tc.remittance.OtherReportsPage" />
    </pages>
</workunit>
