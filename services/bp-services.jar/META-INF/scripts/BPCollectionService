import com.rameses.annotations.*
import com.rameses.common.*
import java.rmi.server.UID

class BPCollectionService{
	
	@Service("BPApplicationReportService")
	def appSvc;
	
	@Service("BPBillingService")
	
	def billingSvc;
	
	@PersistenceContext('main')
	def em;
	
	@Service('DateService')
	def dateService;
	
	@Service('NumberService')
	def numSvc;
	
	@ProxyMethod
	public def getBill( def entity, year, qtr ){
		
		entity.year = (year == 0) ? Integer.parseInt(new java.text.SimpleDateFormat("yyyy").format( dateService.serverDate )) : year;
		entity.qtr = (qtr == 0) ? 4 : qtr;
		
		def app = em.read('bpapplication:bpapplication', [objid:entity.applicationid])
		if( !app ) throw new Exception("Application with objid $entity.applicationid no longer exists.");
		def bp = em.read('business:business', [objid: app.businessid]);
		if( !bp ) throw new Exception("Business with objid $app.businessid no longer exists.");
		
		def bill = [
			ledgerid      : bp.objid,
			yearstarted   : bp.yearstarted,
			lastyearpaid  : bp.lastyearpaid,
			lastqtrpaid   : bp.lastqtrpaid,
			taxfees       : [],
			lobs		  : app.lobs,
			txntype		  : app.txntype, 
		];
		
		def getUnpaidReceivables = { 
			def list = app.receivables.findAll{ numSvc.round( it.amount ) > numSvc.round( it.amtpaid ) };
			list = list.each(){ it.amount -= it.amtpaid }
			if( app.txntype.matches('NEW|ADDLOB') ) return list;
			return list.findAll{ it.iqtr <= entity.qtr }
		}
		
		def receivables = getUnpaidReceivables().each{
			it.year = it.iyear
			it.qtr = it.iqtr
			bill.taxfees << it 
		}
		
		bill = billingSvc.buildBill( bill ); 
		if( !bill.taxfees && entity.qtr == 4 ) throw new Exception( "Business is fully paid." );
		if( !bill.taxfees ) throw new Exception("Business is already paid for quarter " + entity.qtr + '.');
		
		entity.businessid = bp.objid;
		entity.tradename = bp.tradename;
		entity.businessaddress = bp.businessaddress;
		entity.info.payorname = bp.taxpayername;
		entity.info.payoraddress = bp.taxpayeraddress;
		entity.info.paidby = bp.taxpayername;
		entity.info.paidbyaddress = bp.taxpayeraddress;
		entity.info.txndate = dateService.serverDate;
		entity.info.amount = 0.0;
		entity.items = [];
		entity._items = []
		
		def populatePenaltyInfo = { tf, item ->
			if( tf.surcharge > 0.0 ){
				item.surchargetaxfeeid = tf.surchargetaxfeeid
				item.surchargeacctid = tf.surchargeacctid
			}
			if( tf.interest > 0.0 ){
				item.interesttaxfeeid = tf.interesttaxfeeid
				item.interestacctid = tf.interestacctid
			}
		}
		
		bill.taxfees.each{
			def acct = em.read("incomeacct:incomeacct", [objid: it.acctid]);
			def item = [
				taxfeeid	: it.objid,
				iyear		: it.year, 
				iqtr		: it.qtr,  
				lobid		: it.lobid,
				lobname		: it.lobname,
				acctid		: acct.objid, 
				acctno		: acct.acctno,
				accttitle	: acct.accttitle,
				systype 	: acct.systype,
				fundid		: acct.fundid,
				fundname	: acct.fundname,
				surcharge	: new BigDecimal( numSvc.round(it.surcharge ? it.surcharge : 0.0) ),
				discount	: new BigDecimal( numSvc.round(it.discount  ? it.discount  : 0.0) ),
				interest	: new BigDecimal( numSvc.round(it.interest  ? it.interest  : 0.0) ),
				taxamount	: new BigDecimal( numSvc.round(it.amount) ),
				amount 		: new BigDecimal( numSvc.round(it.amount) ),
				
			]
			
			populatePenaltyInfo( it, item )
			def amount = item.taxamount + item.surcharge + item.interest - item.discount
			item.amount = numSvc.round( amount )
			entity.items << item
			entity._items << item
		}
	
		entity.info.amount = numSvc.round( entity._items.amount.sum() ) 
		entity.applicationlastmodified = app.lastmodified;
		entity._items.sort { a, b ->
			def sA = a.iyear+''+a.iqtr
			def sB = b.iyear+''+b.iqtr
			return sA<=>sB 
		}
		
    	return entity;
	}
	
	@ProxyMethod 
	public def doComputePartial( entity , partialpmt ){
		entity._items.each{
			it.surcharge = ( it.surcharge ? it.surcharge : 0.0 )
			it.interest  = ( it.interest  ? it.interest  : 0.0 )
			it.discount  = ( it.discount  ? it.discount  : 0.0 )
			it.partial   = 0
			
			if( partialpmt == 0 )
				it.amount = partialpmt 
			if( partialpmt >= it.amount )
				partialpmt -= numSvc.round( it.taxamount + it.surcharge + it.interest - it.discount )
			else{ 
				it.partial = 1
				it.taxamount = numSvc.round( it.taxamount / it.amount * partialpmt )
				if( it.surcharge > 0.0 ) it.surcharge = numSvc.round( it.surcharge / it.amount * partialpmt )
				//routine tomake ( it.taxamount + it.surcharge + it.interest ) equal to partialpmt
				if( it.interest > 0.0 ) it.interest = numSvc.round( partialpmt - it.taxamount - it.surcharge )
				if( it.discount > 0.0 ) it.discount = numSvc.round( it.discount / it.amount * partialpmt )
				
				it.amount = numSvc.round( it.taxamount + it.surcharge + it.interest - it.discount )
				partialpmt -= it.amount 
			}
		}
		entity._items.sort { a, b ->
			def sA = a.iyear+''+a.iqtr;
			def sB = b.iyear+''+b.iqtr;
			return sA<=>sB;
		}
		return entity._items.findAll{ it.amount > 0.0 }
	}
} 